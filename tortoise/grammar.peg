start = statements

statements
    = statement*

// EXPRESSION

expression
    = expr:comparative
        { return expr; }

// STATEMENTS
statement = 
	 stat_var
    / stat_if
    / stat_define
    / stat_repeat
    / stat_assign
	 / stat_expression

stat_expression = expr:expression ws ";" ws
        { return { tag:"ignore", body:expr }; }

stat_define = 
      "define " ws v:identifier ws "(" ws ")" ws "{" ws body:statements ws "}" ws
        { return { tag:"define", name:v, args:[], body:body }; }
    / "define " ws v:identifier ws "(" ws args:idlist ws ")" ws "{" ws body:statements ws "}" ws
        { return { tag:"define", name:v, args:args, body:body }; }

stat_assign = v:identifier ws ":=" ws expr:expression ws ";" ws
        { return { tag:":=", left:v, right:expr }; }

stat_var = "var " ws v:identifier ws ";" ws
        { return { tag: "var", name:v };}
       
stat_if = "if" ws "(" ws expr:expression ws ")" ws "{" ws body:statements ws "}" ws
        { return { tag:"if", "expr":expr, "body":body}; }

stat_repeat = "repeat" ws "(" ws expr:expression ws ")" ws "{" ws body:statements ws "}" ws
        { return { tag:"repeat", expr:expr, body:body}; }

// NUMBERS

number_frac
    = "." chars:[0-9]*
        { return "." + chars.join(''); }

number
    = head:first_nbr_char chars:[0-9]* frac:number_frac?
        { return parseFloat(head.concat(chars).join('') + frac); }

first_nbr_char
    = head:[\-0-9]
    { return [ head ]; }

// ARITHMETICS 

comp_op = "<=" / ">=" / "!=" / "==" / "<" / ">"

comparative
    = left:additive ws op:comp_op ws right:comparative
        { return {tag: op, left:left, right:right}; }
    / additive

additive_op = "+" / "-"

additive
    = left:multiplicative ws op:additive_op ws right:additive
        { return {tag:op, left:left, right:right}; }
    / multiplicative

mult_op = "*" / "/"

multiplicative
    = left:primary ws op:mult_op ws right:multiplicative
        { return {tag:op, left:left, right:right}; }
    / primary

// PRIMARY / FUNCTION

primary
    = number
    / v:identifier "(" ws ")"
        { return {tag:"call", name:v, args:[]}; }
    / v:identifier "(" ws args:arglist ws ")"
        { return {tag:"call", name:v, args:args}; }
    / "(" ws expr:expression ws ")"
        { return expr; }
    / identifier


var_ref = id:identifier
     { return { tag:"ident", name:id }; }

// IDENTIFIERS

validfirstchar
    = [a-zA-Z_]

validchar
    = [0-9a-zA-Z_]

identifier
    = firstchar:validfirstchar chars:validchar*
        { return firstchar + chars.join(''); }

// AUX

ws = [ \t\n]*

comma_expression = "," ws expr:expression
        { return expr; }

arglist
    = first:expression rest:comma_expression*
        { return [first].concat(rest); }

comma_identifier = "," ws expr:identifier
        {return expr;}

idlist = first:identifier rest:comma_identifier*
        { return [first].concat(rest); }

